
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   	boolean errorDetected;
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

:}

init with {:
	errorDetected = false;
:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROGRAM, READ, PRINT, CONST;
terminal IF, ELSE, FOR, DO, WHILE, BREAK, CONTINUE, RETURN, NEW, VOID;
terminal EQUAL, NOTEQUAL, LESS_OR_EQUAL, GREATER_OR_EQUAL, LESS, GREATER, AND, OR, ASSIGN, INC, DEC, PLUS, MINUS, MUL, DIV, MOD;
terminal SEMI, COMMA, LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET; 

terminal Integer NUM_CONST;
terminal String CHAR_CONST;
terminal String BOOL_CONST;
terminal String IDENT;

nonterminal ProgramDeclList;
nonterminal DeclList, Decl, VarDeclaration, MultipleVarDeclarations, VarDecl, Array, ConstDeclaration, MultipleConstDeclarations, ConstDecl, VarDeclList; 
nonterminal MethodDeclList, MethodDecl, ReturnType, FormalParamList, FormalParamDecl;
nonterminal StatementList, Statement, DesignatorStatement;
nonterminal Matched, Unmatched;
nonterminal ActPars, ActualParamList;
nonterminal Assignop, Relop, Addop, Mulop;
nonterminal Parameters, OptArgs, OptArg;
nonterminal DoWhileStatementBegin, IfConditionBegin, ThenStatementEnd, Or, WhileBegin, WhileStatementBegin, WhileStatementEnd;
nonterminal ForLoopBegin, ForLoopEnd, ForInitialization, ForIteration, ForIterationBegin, ForConditionBegin;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodTypeName, Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Expr, Term, Factor, Const, MulFacList, CondFact, CondTerm, Condition, IfCondition, ActPar, DoWhileCondition, WhileCondition;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ForCondition;

/* Program */

Program ::=	(Program) PROGRAM ProgName:p ProgramDeclList LBRACE MethodDeclList RBRACE;

ProgName ::=	(ProgName) IDENT:programName;

ProgramDeclList ::= (ProgramDecls) DeclList
					|
					(NoProgramDecl) /* epsilon */
					;

/* Variable Declarations */

DeclList ::=	(MultipleDeclarations) DeclList Decl
				|
				(SingleDecl) Decl
				;
				
Decl ::=	(ConstDeclarations) CONST Type:constType ConstDeclaration
			|
			(VarDeclarations) Type:varType VarDeclaration
			;
			
VarDeclList ::=	(VarDeclarationsList) VarDeclList Type:varType VarDeclaration
				|
				/* epsilon */
				;			
			
VarDeclaration ::=	(MultipleVarDecls) MultipleVarDeclarations VarDecl SEMI
					|
					(SingleVarDecl) VarDecl SEMI
					|
					(VarDeclSemiError) error SEMI:l
					{: parser.report_error("Izvrsen oporavak do ; na liniji " + lleft, null); :}
					;
					
MultipleVarDeclarations ::=	(MultipleCommaSeparatedVarDecls) MultipleVarDeclarations VarDecl COMMA
							|
							(FirstVarDecl) VarDecl COMMA
							|
							(VarDeclCommaError) error COMMA:l
							{: parser.report_error("Izvrsen oporavak do , na liniji " + lleft, null); :}
							;
							
VarDecl ::=	(VarDecl) IDENT:varName Array;

Array ::=	(IsArray) LBRACKET RBRACKET
			|
			(NotArray) /* epsilon */
			;

ConstDeclaration ::=	(MultipleConstDecls) MultipleConstDeclarations ConstDecl SEMI
						|
						(SingleConstDecl) ConstDecl SEMI
						|
						(ConstDeclSemiError) error SEMI:l
						{: parser.report_error("Izvrsen oporavak do ; na liniji " + lleft, null); :} 
						;
					
MultipleConstDeclarations ::=	(MultipleCommaSeparatedConstDecls) MultipleConstDeclarations ConstDecl COMMA
								|
								(FirstConstDecl) ConstDecl COMMA
								|
								(ConstDeclCommaError) error COMMA:l
								{: parser.report_error("Izvrsen oporavak do , na liniji " + lleft, null); :}
								;

ConstDecl ::= 	(ConstDecl) IDENT:constName ASSIGN Const:constValue;	

Const ::=	(BoolConst) BOOL_CONST:bool
			|
			(CharConst) CHAR_CONST
			|
			(NumConst) NUM_CONST
			;			

Type ::=	(Type) IDENT:typeName;


/* Method Declarations */

MethodDeclList ::=	(MethodDeclarations) MethodDeclList MethodDecl
					|
					(NoMethodDecl) /* epsilon */
					;
					
MethodDecl ::=	(MethodDecl) MethodTypeName LPAREN Parameters RPAREN VarDeclList LBRACE StatementList RBRACE;

MethodTypeName ::=	(MethodTypeName) ReturnType:retType IDENT:methodName;

ReturnType ::=	(RetType) Type:type
				|
				(RetVoid) VOID
				;
				
FormalParamList ::=	(FormalParamDeclarations) FormalParamList COMMA FormalParamDecl
					|
					(SingleFormalParamDecl) FormalParamDecl 
					;	
					
FormalParamDecl ::=	(FormalParamDeclaration) Type:paramType IDENT:paramName Array
					|
					(FormalParamDeclError) error:l
					{: parser.report_error("Izvrsen oporavak na liniji " + lleft, null); :} 
					;

Parameters ::=	(FormParametersOnly) FormalParamList
				|
				(OptionalArgumentsOnly) OptArgs
				|
				(FormParametersAndOptionalArguments) FormalParamList COMMA OptArgs
				|
				(NoParameters) /* epsilon */
				;

OptArgs ::=	(OptArguments) OptArgs COMMA OptArg
			|
			(SingleOptArg) OptArg
			;		

OptArg ::=	(OptArg) Type:paramType IDENT:paramName ASSIGN Const; 

/* Statements */

StatementList ::=	(Statements) StatementList Statement
					|
					(NoStmt) /* epsilon */
					;
					
Statement ::= 	(MatchedStmt) Matched
				|
				(UnmatchedStmt) Unmatched
				;
				
Unmatched ::=	(UnmatchedIf) IfConditionBegin IfCondition Statement ThenStatementEnd
				|
				(UnmatchedIfElse) IfConditionBegin IfCondition Matched ThenStatementEnd ELSE Unmatched 
				;									
				
IfConditionBegin ::=	(IfConditionBegin) IF;				

ThenStatementEnd ::= (ThenStatementEnd) /* epsilon */;

Matched ::=		(DesignatorStmt) DesignatorStatement SEMI
				|	
				(DesignatorStmtError) error SEMI:l
				{: parser.report_error("Izvrsen oporavak do ; na liniji " + lleft, null);  :}
				|
				(ReadStatement) READ LPAREN Designator RPAREN SEMI
				|
				(PrintStatement) PRINT LPAREN Expr RPAREN SEMI
				|
				(PrintStatementNumConst) PRINT LPAREN Expr COMMA NUM_CONST RPAREN SEMI 
				|
				(BreakStatement) BREAK SEMI
				|
				(ContinueStatement) CONTINUE SEMI
				|
				(ReturnStatement) RETURN SEMI
				|
				(ReturnExprStatement) RETURN Expr:expr SEMI
				|
				(MatchedStatement) IfConditionBegin IfCondition Matched ThenStatementEnd ELSE Matched
				|
				(DoWhileStatement) DoWhileStatementBegin Matched WhileBegin LPAREN DoWhileCondition RPAREN SEMI
				|
				(WhileStatement) WhileStatementBegin LPAREN WhileCondition RPAREN Matched WhileStatementEnd
				|
				(ForLoop) ForLoopBegin LPAREN ForInitialization ForConditionBegin ForCondition ForIterationBegin ForIteration RPAREN Matched ForLoopEnd 
				|
				(Stmt) LBRACE StatementList RBRACE
				;
				
ForLoopBegin ::=	(ForLoopBegin) FOR;

ForLoopEnd ::=	(ForLoopEnd) /* epsilon */;	

ForInitialization ::=	(ForInitialization) DesignatorStatement SEMI;	

ForConditionBegin ::= (ForConditionBegin) /* epsilon */;

ForCondition ::= (ForCondition) Condition SEMI;	

ForIterationBegin ::= (ForIterationBegin) /* epsilon */;

ForIteration ::= (ForIteration) DesignatorStatement;
				
WhileStatementBegin ::= (WhileStatementBegin) WHILE;				

WhileStatementEnd ::= (WhileStatementEnd) /* epsilon */;
				
WhileBegin ::=	(WhileBegin) WHILE;
				
DoWhileStatementBegin ::=	(DoWhileStatementBegin) DO;		

WhileCondition ::= 	(WhileCondition) Condition;

DoWhileCondition ::=	(DoWhileCondition) Condition;		
				
DesignatorStatement ::=	(DesignatorAssignStatement) Designator:designator Assignop Expr:expr
						|
						(DesignatorIncStatement) Designator INC
						|
						(DesignatorDecStatement) Designator DEC
						|
						(DesignatorFunctionCall) Designator LPAREN ActPars RPAREN
						;
							

Designator ::=	(DesignatorIdent) IDENT 
				|
				(DesignatorIdentArray) Designator LBRACKET Expr RBRACKET
				;		
				
Expr ::=	(AddOpTermExpr) Expr:te Addop Term:t
			|
			(NegTermExpr) MINUS Term
			|
			(PosTermExpr) Term:term
			;
			
Term ::=	(Term) Factor:f MulFacList;

MulFacList ::= 	(MulopFactor) MulFacList Mulop Factor 
				|
				(NoMulopFactor) /* epsilon */
				;
						
			
Factor ::= 	(ConstFactor) Const:constant
			|
			(ExprFactor) LPAREN Expr RPAREN
			|
			(NewArray) NEW Type:arrayType LBRACKET Expr RBRACKET
			|
			(Var) Designator:designator
			|
			(FunctionCall) Designator:designator LPAREN ActPars RPAREN
			|
			(NewObj) NEW Type:objType
			;	
			
ActPars ::=	(ActualPararameters) ActualParamList
			|
			(NoActualPars) /* epsilon */
			;					
				
ActualParamList ::=	(ActualParams) ActualParamList COMMA ActPar
					|
					(ActualParam) ActPar
					;
					
ActPar ::= (ActPar) Expr;					
					
/* Conditions */

IfCondition ::=	(IfCond) LPAREN Condition RPAREN
				|
				(IfCondError) error RPAREN:l
				{: parser.report_error("Izvrsen oporavak do ) na liniji " + lleft, null); :}
				;

Condition ::=	(MultipleCondTerms) Condition Or CondTerm
				|
				(SingleCondTerm) CondTerm
				;

Or ::= (Or) OR;

CondTerm ::=	(MultipleCondFacts) CondTerm AND CondFact
				|
				(SingleCondFact) CondFact
				; 

CondFact ::=	(CondFactExpr) Expr
				|
				(CondFactRelopExpr) Expr Relop Expr
				; 												


/* Operators */

Assignop ::=	(Assignop) ASSIGN;											

Relop ::=	(RelopEqual) EQUAL
			|
			(RelopNotEqual) NOTEQUAL
			|
			(RelopLessOrEqual) LESS_OR_EQUAL
			|
			(RelopGreaterOrEqual) GREATER_OR_EQUAL
			|
			(RelopLess) LESS
			|
			(RelopGreater) GREATER
			;

Addop ::= 	(AddopPlus) PLUS
	  		|
	  		(AddopMinus) MINUS
	  		;
	  
Mulop ::= 	(MulopMul) MUL
 	   	   	|
 	   		(MulopDiv) DIV
 	   		|
 	   		(MulopMod) MOD
 	   		;
 	   		